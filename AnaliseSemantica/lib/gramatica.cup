package analysis;

import java_cup.runtime.*;
import java.io.*;
import java.util.Map;
import java.util.HashMap;
import java.util.LinkedList;
import java.util.Deque;

action code {:
	Map<String,Integer> ts = new HashMap<String,Integer>();
	Deque<Integer> pTipos = new LinkedList<Integer>();
	
	public void asVarCheck(String ident) throws Exception {
		Integer t = asUnstackType();
		if(ts.containsKey(ident)) {
			throw new Exception("Variável \"" + ident + "\" já declarada");
		} else {
			ts.put(ident, t);
			System.out.println("ID <" + ident + "," + Sym.terminalNames[t] + "> adicionado à tabela de símbolos");
		}
	}
	
	public void asStackUpType(Integer varType) {
		pTipos.addFirst(varType);
		System.out.println("Tipo empilhado: " + Sym.terminalNames[varType]);
	}
	
	public Integer asUnstackType() throws Exception {
		if (pTipos.isEmpty()) {
			throw new Exception("Pilha vazia");
		}
		Integer t = pTipos.removeFirst(); 
		System.out.println("Tipo desempilhado: " + Sym.terminalNames[t]);
		return t;
	}
	
	public void asStackUpResultingType_SUM_SUB() throws Exception {
		Integer aType = asUnstackType();
		Integer bType = asUnstackType();
		Integer resultingType;
		if (aType == Sym.IntType && bType == Sym.IntType) {
			resultingType = Sym.IntType;
		} else {
			resultingType = Sym.FloatType;
		}
		asStackUpType(resultingType);
	}
	
	public void asCheckCompatibilityATT_CMD(String id) throws Exception {
		Integer expType = asUnstackType();
		Integer idType = asUnstackType();
		if (idType == Sym.IntType && expType == Sym.FloatType) {
			String idTypename = Sym.terminalNames[idType];
			String expTypename = Sym.terminalNames[expType]; 
			throw new Exception("A variável '" + id + "' do tipo " +
				 idTypename + " não pode armazenar valores do tipo "
				 + expTypename);
		}
	}
:};

terminal IntType, FloatType, EndCmd, Att;
terminal SumOp, SubOp, OpenPar, ClosePar;
terminal Integer, Float, Id;

non terminal INIT, COMMANDS, CMD, ATT_CMD, DEC_ATT_CMD, TYPE;
non terminal DEC_CMD, EXPRESSION, ARITHMETIC_EXP, ARITHM_EXP;
non terminal FACTOR, NUMBER, SUM_SUB_OP;

start with INIT;

INIT ::= COMMANDS;
COMMANDS ::= CMD EndCmd COMMANDS | ;
CMD ::= DEC_CMD | ATT_CMD | DEC_ATT_CMD;

DEC_CMD ::= TYPE Id:ident
			{:
				asVarCheck((String)ident);
			:}
			;
ATT_CMD ::= Id:ident
			{:
				asStackUpType(ts.get(ident));
			:}
			Att EXPRESSION
			{:
				asCheckCompatibilityATT_CMD((String)ident);
			:}
			;

DEC_ATT_CMD ::= DEC_CMD Att EXPRESSION ;

TYPE ::= IntType
		 {:
		 	asStackUpType(Sym.IntType);
		 :} 
		 | FloatType
		 {:
		 	asStackUpType(Sym.FloatType);
		 :} 
		 ;

EXPRESSION ::= ARITHMETIC_EXP;
ARITHMETIC_EXP ::= FACTOR ARITHM_EXP;
ARITHM_EXP ::= SUM_SUB_OP FACTOR 
			{:
				asStackUpResultingType_SUM_SUB();
			:}
			ARITHM_EXP	| ;
FACTOR ::= NUMBER 
		   | Id:ident
		   {:
	   	   		asStackUpType(ts.get(ident));
		   :}	
		   | OpenPar ARITHMETIC_EXP ClosePar ;
NUMBER ::= Integer
		   {:
		   		asStackUpType(Sym.IntType);
		   :} 
	       | Float 
	       {:
	       		asStackUpType(Sym.FloatType);
	       :}
	       ;

SUM_SUB_OP ::= SumOp | SubOp ;

